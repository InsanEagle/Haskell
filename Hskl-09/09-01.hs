   -- Демонстрация моделирования базисных
   -- комбинаторов на языке Haskell
   -- **********************************
   -- Комбинатор B (композитор) выражает
   -- композицию функций f и g:
   --
   --   Bfg=f(gx)
   -----------------
   b f g x = f (g x)

   -- ****************************************
   -- Комбинатор C (коммутатор) меняет порядок
   -- следования аргументов функции f:
   --
   --   Cfxy=fyx
   ---------------
   c f x y = f y x

   -- ********************************************
   -- Комбинатор D (комбинатор упорядоченной пары)
   -----------------------------------------------
   d x y z = z (k y) x

   -- ************************************
   -- Комбинатор I (комбинатор тождества):
   --
   --   If=f
   -----------
   i f x = f x

   -- ************
   -- Комбинатор J 
   -----------------------
   j x y z w = x y (x w z)

   -- ********************************************
   -- Комбинатор K (канцелятор) выражает константу
   -- c как функцию аргумента x:
   --
   --   Kcx=c
   ----------
   k c x = c

   -- ***************************************
   -- Комбинатор S (коннектор) "распределяет"
   -- аргумент x по функциям f и g:
   --
   --   Sfgx=fx(gx)
   -------------------
   s f g x = f x (g x)

   -- **********************************************
   -- Комбинатор Sp (комбинатор прибавления единицы)
   -------------------------------------------------
   sp u x y = x (u x y)

   -- ***********************************
   -- Комбинатор W (дупликатор) дублирует
   -- второй аргумент:
   --
   --   Wfx=fxx
   -------------
   w f x = f x x

   -- **********************************
   -- Комбинатор Z0 моделирует нумерал 0
   -- в нумерации Чёрча
   --------------------
   z0 x y = y

   -- **************
   -- Комбинатор phi
   ---------------------------
   phi x y z w = x (y w) (z w)

   -- ********************************************
   -- Комбинатор psi. Его аналогом в GHCi является
   -- функция on.
   --  > :m Data.Function
   --  > :t on
   --  (b -> b -> c) -> (a -> b) -> (a -> a -> c)
   ----------------------------------------------
   psi x y z w = x (y z) (y w)
   psi'        = \x y z w -> x (y z) (y w)

   -- ******************************************
   -- Тестирование комбинаторов B, C, I, K, S, W
   ---------------------------------------------
   test1 =   b sqrt maximum [4,2,-5,25] == 5.0
          && c (-) 2 3                  == 1
          && c (-) 3 2                  == (-1)
          && i head [5,4,3,2,1]         == 5
          && k 2 3                      == 2
          && s (+) abs (-5)             == 0
          && w (+) 3                    == 6
   -----------------------------------------------
   test2 = phi (+) ((** 2) . sin) ((** 2) . cos) 1

   -- ********************************************************
   -- Варианты эквивалентной записи (с точки зрения семантики)
   -- функции w=f(g(h(x,y,z))) в бесточечной нотации 
   --------------------------------------------------------------
   w'   = b (b (b (b f g))) h         -- с помощью комбинатора b;
   w''  = (.) ((.) ((.) ((.) f g))) h -- с помощью операции (.);
   w''' = (((f . g) . ) . ) . h       -- с помощью сечения компо-
                                      -- зиции
   h x y z = x + y + z                -- вспомогательная функция
   f x     = x^2                      -- вспомогательная функция
   g x     = x*3                      -- вспомогательная функция
   -------------------------------------------------------------
   test3 =   w'  1 2 3 == w''  1 2 3
          && w'' 3 2 1 == w''' 3 2 1

   -- *****************************************************
   -- Запись функции r=f1 (f2 x y) z в бесточечной нотации:
   --
   -- r=f1 (f2 x y) z = (.) f1 (f2 x) y z = 
   --                 = (.) ((.) f1) f2 x y z = (f1 .) . f2 x y z
   --------------------------------------------------------------
   r' = (f1 . ) . f2
      where f1 x y = x^y
            f2 x y = x+y
   ---------------------------------
   test4 = r' 31 42 53 == (31+42)^53
