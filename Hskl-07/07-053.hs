   -- Демонстрация:
   --  (1) применения развёртки unfoldr на списках
   --      (из модуля List);
   --  (2) моделирования развёртки unfoldr с помощью
   --      класса типов Maybe;
   --  (3) реализации некоторых функционалов с помо-
   --      щью построенной развёртки unfoldr'
   --
   -- (ч е т в ё р т ы й  вариант реализации unfold)
   -- **********************************************
   import List
   import Random

   -- ************************************************
   -- Применение развёртки unfoldr из библиотеки List:
   --  (1) для генерации списка вида [n,n+1,...,m];
   --  (2) для построения функции, возвращающей строку
   --      битов заданного числа n
   --------------------------------
   enumUnfold:: Int -> Int -> [Int]
   enumUnfold n m = unfoldr (\x -> if x>m 
                                     then Nothing 
                                     else Just (x,x+1)) n
   ------------------------------------------------------
   toBinary = reverse . unfoldr step
        where step n = if n==0
                        then Nothing
                        else Just (intToDigit $ mod n 2,
                                   div n 2)

   -- ****************************************
   -- Функция, моделирующая функционал unfoldr
   -- из модуля List
   ------------------------------------------
   unfoldr' :: (b -> Maybe (a,b)) -> b -> [a]
   unfoldr' f b = case (f b) of
                   Just (a,b') -> a : unfoldr' f b'
                   Nothing     -> []

   -- *********************************************
   -- Реализация функционала zip с помощью unfoldr'
   ------------------------------------------------
   zip' :: [a] -> [b] -> [(a,b)]
   zip' = curry $ 
           unfoldr' $ \x -> case x of
                             ([],  _)    -> Nothing
                             (_,   [])   -> Nothing
                             (a:as,b:bs) -> Just ((a,b),(as,bs))

   -- *************************************************
   -- Реализация функционала iterate с помощью unfoldr' 
   ----------------------------------------------------
   iterate' :: (a -> a) -> a -> [a]
   iterate' f = unfoldr' (\s -> Just (s,f s))

   -- ************************************************
   -- Реализация сортировки выбором с помощью unfoldr' 
   ---------------------------------------------------
   minSort :: Ord a => [a] -> [a]
   minSort = unfoldr' (\x -> if null x 
                               then Nothing
                               else Just (minimum x, 
                                          delete (minimum x) x))

   -- ***************************
   -- Неудачные тестовые примеры:
   ---------------------------------------------------------
   lstRnd = take 100 $ randomRs (1,10) (mkStdGen 7) :: [Int]
   ---------------------------------------------------------
   test1 = enumUnfold
   test2 = toBinary 1337 == "10100111001"
   test3 = take 20 (unfoldr  (\x -> Just (x,x+1)) 1)
   -------------------------------------------------
   test4 = take 20 (unfoldr' (\x -> Just (x,x+1)) 1)
   test5 = zip' [1..6] [7..12]
   test6 = take 10 (iterate' (\x -> x^2) 2)
   test7 = minSort [23,21..1] == sort [23,21..1]
   test8 = minSort lstRnd
